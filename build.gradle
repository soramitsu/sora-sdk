plugins {
    id 'java'
    id 'idea'
    id 'jacoco'
    id 'groovy'
    id "org.sonarqube" version "2.7.1"
}

group = 'jp.co.soramitsu'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

jacoco {
    toolVersion = "0.8.1"
    reportsDir = file("$buildDir/reports/")
}

jacocoTestReport {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    reports {
        xml.enabled true
        xml.destination file("${buildDir}/reports/coverage.xml")
        csv.enabled false
        html.enabled true
        html.destination file("${buildDir}/reports/html")
    }

    dependsOn(test)
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.10'
}

repositories {
    jcenter()
    mavenCentral()
    maven { url "https://jitpack.io" }
}

final def spockVersion = '1.1-groovy-2.4'
final def edVersion = '2.0.1'
final def spockGenesisVersion = '0.6.0'
final def jacksonVersion = '2.10.0'
final def onecodeVersion = '2.0.2'
final def lombokVersion = '1.18.8'
final def cglibVersion = '3.2.12'
final def bytebuddyVersion = '1.9.13'
final def objgenesisVersion = '3.0.1'
final def validationVersion = '2.0.1.Final'
final def jaywayVersion = '2.4.0'
final def jcipVersion = '1.0.0'
final def hibernateValidatorVersion = '6.1.5.Final'
final def elVersion = '3.0.3'


dependencies {
    // for mocking in groovy
    testCompile("net.bytebuddy:byte-buddy:${bytebuddyVersion}")
    testCompile("org.objenesis:objenesis:${objgenesisVersion}")
    testCompile("cglib:cglib-nodep:${cglibVersion}")

    compileOnly("org.projectlombok:lombok:${lombokVersion}")
    testCompileOnly("org.projectlombok:lombok:${lombokVersion}")
    testCompile("com.nagternal:spock-genesis:${spockGenesisVersion}")
    testCompile("org.spockframework:spock-core:${spockVersion}")
    testCompile("com.jayway.jsonpath:json-path:${jaywayVersion}")
    testCompile("org.hibernate.validator:hibernate-validator:${hibernateValidatorVersion}")
    testCompile("org.glassfish:jakarta.el:${elVersion}")

    compile("javax.validation:validation-api:${validationVersion}")
    compile("org.lucee:jcip-annotations:${jcipVersion}")

    compile "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}"
    compile "com.fasterxml.jackson.core:jackson-annotations:${jacksonVersion}"
    compile "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${jacksonVersion}"

    compile("com.github.warchant:ed25519-sha3-java:${edVersion}")
    compile("com.github.soramitsu:1code-java:${onecodeVersion}")
}

sonarqube {
    properties {
        property "sonar.exclusions", "**/generated/*.java"
    }
}
